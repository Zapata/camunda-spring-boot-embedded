allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	project.sourceCompatibility = 1.7
	project.targetCompatibility = 1.7

	group = 'com.github.zapata'
	version = '0.0.1-SNAPSHOT'
	
	repositories {
		mavenCentral()
		
		maven {
			name 'Camunda Repository'
			url 'https://app.camunda.com/nexus/content/groups/public'
		}
	}
	
	project.ext.camundaVersion = '7.2.0'
}


buildscript {
	repositories {
		mavenCentral()
	}
	
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
    }
    configurations {
        compile.exclude module: "spring-boot-starter-tomcat"
       	compile.exclude module: "tomcat-jdbc"
    }
}

apply plugin: 'spring-boot'

jar {
    baseName = 'camunda-spring-boot-embedded'
    version = '0.0.1-SNAPSHOT'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jersey")
    compile("org.springframework.boot:spring-boot-starter-undertow")
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    //compile("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")

	// Camunda
    // 7.2.0 not working at least because of: CAM-3174, CAM-3167.
	compile("org.camunda.bpm:camunda-engine:7.3.0-alpha1") {
		exclude group: 'org.apache.commons', module: 'commons-email'
	}
	compile("org.camunda.bpm:camunda-engine-spring:$camundaVersion")
	compile("org.camunda.bpm:camunda-engine-rest:$camundaVersion:classes")
	compile("org.camunda.bpm.webapp:camunda-webapp:$camundaVersion:classes")
	compile project(":camunda-webapp-webjar")
	

	// Database
	compile("com.h2database:h2:1.3.175")
	compile("com.zaxxer:HikariCP-java6")
	
    // Utilities
    compile("com.google.guava:guava:18.0")
    // AssertJ
    testCompile("org.assertj:assertj-core:1.7.0")
    testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("com.jayway.restassured:rest-assured:2.3.1")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}